apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "com.wisn.skin1"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode getGitVersion()
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    signingConfigs {
        release {
            storeFile file(RELEASE_STOREFILE)
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
            storePassword RELEASE_STOREPASSWORD
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def buildType = variant.buildType.name
            def outputFile = output.outputFile
            def date = releaseTime()
            def applicationId = defaultConfig.applicationId
            def versionName = defaultConfig.versionName
            def versionCode = defaultConfig.versionCode
            if (outputFile != null && buildType.contains("release") && outputFile.name.endsWith('.apk')) {
                def fileName = "theme-${applicationId}-${variant.flavorName}-${versionCode}-${versionName}-${date}.skin"
//                output.outputFile = new File(outputFile.parent, fileName)
                output.outputFile = new File("$rootDir/apkFile", fileName)

            }
        }
    }
}

def releaseTime() {
    return new Date().format("yyyy-MM-dd-hh-mm-ss", TimeZone.getTimeZone("Asia/Shanghai"))
}

def getGitVersion() {
    return 'git rev-list HEAD --count'.execute().text.trim().toInteger()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
}
